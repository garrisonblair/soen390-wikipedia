apply plugin: 'com.android.application'


import com.android.build.gradle.internal.dsl.ProductFlavor
import com.android.ddmlib.DdmPreferences

import java.util.concurrent.TimeUnit

// Copy the signing.properties.sample file to ~/.sign/signing.properties and adjust the values.
final File PROD_PROPS_FILE = new File(System.getProperty('user.home'), '.sign/signing.properties')
final File REPO_PROPS_FILE = new File('repo.properties')
final Properties PROD_PROPS = loadProperties(PROD_PROPS_FILE)
final Properties REPO_PROPS = loadProperties(REPO_PROPS_FILE)

final int ADB_TIMEOUT = TimeUnit.MINUTES.toMillis(15)
final boolean continuousIntegrationBuild = System.getenv('JENKINS_HOME') != null
final boolean preDexEnabled = hasProperty('pre.dex') ?
        Boolean.valueOf(getProperty('pre.dex').toString()) :
        !continuousIntegrationBuild
if (!preDexEnabled) {
    println 'Pre-dexing disabled.'
}

if (continuousIntegrationBuild) {
    DdmPreferences.setTimeOut(ADB_TIMEOUT)
    println "Device timeout is ${DdmPreferences.getTimeOut()}ms"
}

def static getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy-MM-dd')
    return formattedDate
}

def computeVersionName(label) {
    return "2.7.${android.defaultConfig.versionCode}-${label}-${date}"
}

final JavaVersion JAVA_VERSION = JavaVersion.VERSION_1_8
android {
    // Keep version in sync with /project.properties
    compileSdkVersion 27
    buildToolsVersion '27.0.1'

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility = JAVA_VERSION
        targetCompatibility = JAVA_VERSION
    }

    dexOptions {
        preDexLibraries = preDexEnabled
        jumboMode = true
    }

    defaultConfig {
        applicationId 'org.wikipedia'
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 224
        testApplicationId 'org.wikipedia.test'
        testInstrumentationRunner 'org.wikipedia.test.AndroidTestRunner'
        // Added for google vision. may not be necessary
        multiDexEnabled true
//        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments('butterknife.minSdk': minSdkVersion.apiString,
                        'butterknife.debuggable': 'false')
            }
        }
    }

    sourceSets {
        test {
            java.srcDirs += 'src/testlib/java'
        }
        androidTest {
            java.srcDirs += 'src/testlib/java'
        }
    }

    signingConfigs {
        prod {
            setSigningConfigKey(prod, PROD_PROPS)
        }
        debug {
            setSigningConfigKey(debug, REPO_PROPS)
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFile 'test-proguard-rules.pro'
        }
    }

    flavorDimensions 'default'

    productFlavors {
        dev {
            versionName computeVersionName('dev')
            applicationIdSuffix 'dev'
            signingConfig signingConfigs.debug
            testInstrumentationRunner 'org.wikipedia.test.AndroidTestRunner'
            dimension 'default'
        }
        prod {
            versionName computeVersionName('r')
            signingConfig signingConfigs.prod
            dimension 'default'
        }
        releasesprod {
            versionName computeVersionName('releasesprod')
            signingConfig signingConfigs.prod
            dimension 'default'
        }
        alpha {
            versionName computeVersionName('alpha')
            applicationIdSuffix ''
            signingConfig signingConfigs.debug
            testInstrumentationRunner 'org.wikipedia.test.AndroidTestRunner'
            dimension 'default'
        }
        beta {
            versionName computeVersionName('beta')
            applicationIdSuffix 'beta'
            signingConfig signingConfigs.prod
            dimension 'default'
        }
        amazon {
            versionName computeVersionName('amazon')
            signingConfig signingConfigs.prod
            dimension 'default'
        }
        fdroid {
            versionName computeVersionName('fdroid')
            signingConfig signingConfigs.prod
            dimension 'default'
        }
        custom {
            versionName computeVersionName(customChannel)
            // next line is for injecting a custom channel value into the custom/AndroidManifest.xml
            manifestPlaceholders = [customChannel: getProperty('customChannel').toString()]
            signingConfig signingConfigs.prod
            dimension 'default'
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        // required by Butter Knife

        // For Espresso testing libraries. See http://stackoverflow.com/q/33800924/970346.
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

apply from: '../gradle/src/test.gradle'
apply from: '../gradle/src/checkstyle.gradle'
apply from: '../gradle/src/screenshot.gradle'

repositories {
    mavenCentral()
}

dependencies {

    // To keep the Maven Central dependencies up-to-date
    // use http://gradleplease.appspot.com/ or http://search.maven.org/.
    // Debug with ./gradlew -q app:dependencies --configuration compile

    String okHttpVersion = '3.9.1' // When updating this version, resync file copies under
    // app/src/main/java/okhttp3
    String retrofitVersion = '2.3.0'
    String supportVersion = '27.0.2'
    String espressoVersion = '2.2.2'
    String butterKnifeVersion = '8.8.1'
    String frescoVersion = '1.5.0' // When updating this version, resync file copies under
    // app/src/main/java/com/facebook
    String testingSupportVersion = '0.5'
    String mockitoCore = 'org.mockito:mockito-core:1.9.5'
    String leakCanaryVersion = '1.5'

    implementation "com.android.support:support-compat:$supportVersion"
    implementation "com.android.support:support-core-utils:$supportVersion"
    implementation "com.android.support:support-core-ui:$supportVersion"
    implementation "com.android.support:support-fragment:$supportVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    implementation "com.android.support:cardview-v7:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support:recyclerview-v7:$supportVersion"
    implementation "com.android.support:palette-v7:$supportVersion"
    implementation "com.android.support:preference-v14:$supportVersion"

    implementation "com.facebook.fresco:animated-gif:$frescoVersion"
    implementation "com.facebook.fresco:fresco:$frescoVersion"
    implementation "com.facebook.fresco:imagepipeline-okhttp3:$frescoVersion"
    implementation 'com.google.code.gson:gson:2.8.1'
    implementation 'com.romandanylyk:pageindicatorview:0.1.2'
    implementation "com.squareup.okhttp3:okhttp-urlconnection:$okHttpVersion"
    // for JavaNetCookieJar
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    implementation 'com.squareup:otto:1.3.8'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.9.1'
    implementation "com.jakewharton:butterknife:$butterKnifeVersion"
    implementation "com.mapbox.mapboxsdk:mapbox-android-sdk:5.2.0@aar"
    implementation "com.mapbox.mapboxsdk:mapbox-android-ui:2.2.9"
    implementation 'net.hockeyapp.android:HockeySDK:4.1.3'
    implementation 'org.apache.commons:commons-lang3:3.5'
    implementation "commons-io:commons-io:2.5"
    implementation 'org.jsoup:jsoup:1.10.2'
    implementation 'com.dmitrybrant:zimdroid:0.0.14'

    // Also seems to be needed for Android 27 of greater
    implementation 'com.google.code.findbugs:jsr305:3.0.0'
    // Start of what may be needed for google vision based on examples in good app
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:27.0.2'
    compile 'com.android.support:design:27.0.2'
    compile 'com.google.api-client:google-api-client-android:1.22.0' exclude module: 'httpclient'
    compile 'com.google.http-client:google-http-client-gson:1.20.0' exclude module: 'httpclient'
    compile 'com.google.apis:google-api-services-vision:v1-rev2-1.21.0'
    // End of what may be needed for google vision based on examples in good app

    // Added YouTube Data API Client Library in order to search and play YouTube videos
    compile 'com.google.api-client:google-api-client:1.23.0'
    compile 'com.google.apis:google-api-services-youtube:v3-rev193-1.23.0'

    // Added YouTube Player API dependencies in order to play video content obtained from API
    compile files('libs/YouTubeAndroidPlayerApi.jar')

    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnifeVersion"

    android.productFlavors.each { flavor ->
        String dep
        if ('dev' == flavor.name) {
            dep = "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
        } else {
            dep = "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
        }
        "${flavor.name}Implementation" dep
    }

    testImplementation 'junit:junit:4.12'
    testImplementation mockitoCore
    testImplementation 'org.robolectric:robolectric:3.3.2'
    testImplementation "com.squareup.okhttp3:mockwebserver:$okHttpVersion"

    androidTestImplementation mockitoCore
    androidTestRuntimeOnly 'com.crittercism.dexmaker:dexmaker:1.4'
    androidTestImplementation 'com.crittercism.dexmaker:dexmaker-mockito:1.4'
    androidTestRuntimeOnly 'com.crittercism.dexmaker:dexmaker-dx:1.4'

    // Required by Android JUnit Runner.
    androidTestImplementation "com.android.support:support-annotations:$supportVersion"
    androidTestImplementation "com.android.support.test:rules:$testingSupportVersion" // JUnit Rules
    androidTestImplementation "com.android.support.test:runner:$testingSupportVersion"
    // Android JUnit Runner
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-intents:2.2') {
        // Necessary to avoid version conflicts
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    testCompile 'junit:junit:4.12'
    testCompile 'org.powermock:powermock-api-mockito:1.6.1'
    testCompile 'org.powermock:powermock-module-junit4-rule-agent:1.6.1'
    testCompile 'org.powermock:powermock-module-junit4-rule:1.6.1'
    testCompile 'org.powermock:powermock-module-junit4:1.6.1'

    //Room
    implementation "android.arch.persistence.room:runtime:1.0.0"
    annotationProcessor "android.arch.persistence.room:compiler:1.0.0"

    // Test helpers for Room
    testImplementation "android.arch.persistence.room:testing:1.0.0"

    // Required by Espresso
//    androidTestCompile 'com.android.support.test.espresso:espresso-core:3.0.1'
//    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:3.0.1'
//    androidTestCompile 'com.android.support.test.espresso:espresso-intents:3.0.1'
//    androidTestCompile 'com.android.support.test.espresso:espresso-accessibility:3.0.1'
//    androidTestCompile 'com.android.support.test.espresso:espresso-web:3.0.1'
//    androidTestCompile 'com.android.support.test.espresso.idling:idling-concurrent:3.0.1'
//    androidTestCompile 'com.android.support.test:runner:1.0.1'
//
//    androidTestCompile 'com.android.support.test.espresso:espresso-idling-resource:3.0.1'
}

// Configuration for Sonarqube static analysis tool
// Need to download: Latest LTS (SonarQube 6.7.2 (LTS*) from https://www.sonarqube.org/downloads/
// Then Unzip and traverse to <download location>/bin/<your operating system folder>
// Run sonar.sh or whatever your sonar sonar script and once it is up and running open on https://localhost:9000
// Login with Username: admin Password: admin
// To run static analysis use the commands below. Please note you will need to use the token you get when you sign into your sonarqube instance
// ./gradlew sonarqube   -Dsonar.host.url=http://localhost:9000   -Dsonar.login=3e1b700c923b698f2abed7abec5ba76e5211f835
// Properties can be be configured in the sonar.properties file in: <download location>/bin/<your operating system folder>/sonar.properties
// Additional configuration can be found here: https://docs.sonarqube.org/display/SCAN/Analyzing+with+SonarQube+Scanner+for+Gradle
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
    }
}

apply plugin: "org.sonarqube"

// Default is debug but i think we should use devDebug
// Alternate candidates include: amazonDebug, amazonRelease, releasesprodDebug, releasesprodRelease, fdroidDebug, fdroidRelease, prodDebug, prodRelease, devDebug, devRelease, alphaDebug, alphaRelease, customDebug, customRelease, betaDebug, betaRelease
/*
sonarqube {
    androidVariant 'devDebug'
}
*/

private setSigningConfigKey(config, Properties props) {
    if (props) {
        config.storeFile = props['keystore'] == null ? null : file(props['keystore'])
        config.storePassword = props['store.pass']
        config.keyAlias = props['key.alias']
        config.keyPassword = props['key.pass']
    }
    return config
}

private static Properties loadProperties(File file) {
    Properties props = null
    if (file.canRead()) {
        props = new Properties()
        props.load(new FileInputStream(file))
    } else {
        System.err.println "\"${file}\" not found"
    }
    return props
}

private void addSyncContentProviderAuthority(String path, String name) {
    android.productFlavors.all { ProductFlavor flavor ->
        String authority = "${appId(flavor)}.sync.${path}"
        resValue 'string', "${name.toLowerCase()}_authority", authority
        buildConfigField 'String', "${name.toUpperCase()}_AUTHORITY", "\"$authority\""
    }
}

private String appId(ProductFlavor flavor) {
    String suffix = flavor.applicationIdSuffix
    String id = android.defaultConfig.applicationId + (suffix ? ".$suffix" : '')
    return id
}
